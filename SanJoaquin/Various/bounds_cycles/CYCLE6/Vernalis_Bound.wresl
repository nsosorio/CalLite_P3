! Edward Chang
! 5/01/2000
! Joint Model
! Revised by C. Hsu, 4/10/03, USBR, for review and documentation project
! This code bounds decision variables using monthly weighted constraints for cycle 6 only (within San Joaquin Basin)
!###################################################################################################

/*
Flow at Vernalis, without Vernalis minimum flows from February to June plus October
*/
define Vern_nomincycle6 {
    case NonPulseNonDO {
        condition   month <= mar
        value       C_SJRVER[SJR_WQ1] }
    case April {
        condition   month == apr
        value       14.*C_SJRVER[SJR_WQ1]/30. + 16.*C_SJRVER[VAMP_AND_DO]/30. }
    case Mayonly {
        condition   month == may
        value       15.*C_SJRVER[VAMP_AND_DO]/31. + 16.*C_SJRVER[SJR_WQ1]/31. }
    case NonPulsePlusDO {
        condition   always
        value       C_SJRVER[VAMP_AND_DO] }
        }

! Upper SJR operations can create cycle to cycle changes in Chowchilla Bypass
! losses that result in cycle 6 solutions to upper basin flows that affect Vernalis.  
! If these differences are not accounted for then they come out of New Melones, 
! which is not correct.

! If the Chowchilla bypass use flips from on to off to on based on SJRR pulse flows, 
! the effect on loss (L_SJRCB) needs to be accommodated
define L_SJRCBmod {std kind 'mod' units 'cfs'}
goal setL_SJRCBmod{
    lhs L_SJRCBmod
    case AprilChowBPFlip {
        condition month==APR .and. L_SJRCB[sjrbase]>L_SJRCB[vamp_and_do]
        rhs 14.*C_SJRLndr[sjrbase]/30.+16.*C_SJRLndr[vamp_and_do]/30.-C_SJRLndr}
    case otherwise {
        condition always
        rhs 0. }
}

! Total flows at Vernalis including instream and minimum flows
 goal set1Vernalis_cycle6 { C_SJRVER = Vern_nomincycle6 /*+ C_MELONINSTREAM*/ + C_MELONRPA + C_MELONMIN + C_MCLREMIN - L_SJRCBmod }


/*
Vernalis flows during pulse (April and May including ERPP), and non-pulse periods
for use in computing final water quality based on split month calculations
ERPP inflows will affect Vernalis flows in May
*/
! ERPP inflows for 10 days, units = CFS
define SJRERPPinflows { value     0. }


/*
15 day water representing cycle pulse period; weights ERPP for 15 days
does not include D_SJRVER (Vernalis Non-project Diversion)
*/
define pulseC_SJRVERwoD_SJRVER {
     case April {
          condition month == apr
          value     C_SJRVER[VAMP_AND_DO] }
     case MayOnly {
          condition  month == may
          value     C_SJRVER[VAMP_AND_DO] + SJRERPPinflows*10./15.}
     case NotAprMay {
          condition always
          value     0. }
          }
! Vernalis non-pulse flow before the Vernalis Non-project Diversion
define flowC_SJRVERwoD_SJRVER {
     case OctthruMay {
          condition month <= may
          value C_SJRVER[SJR_WQ1] + VernMin_reqtobemet + MercedRelease_Oct*TAF_cfs }
     case DOwindow {
          condition always
          value     C_SJRVER[VAMP_AND_DO] + VernMin_reqtobemet}
          }
