/*************************************
Banks_pump_allow.wresl
 
Nazrul Islam DWR (04/27/2010)

Defines Standard allowable & Max allowable pumping at Banks.  Banks can increase pumping
up to 1/3 of San Joaquin River flow at Vernalis if flow at Vernalis is greater than 1000 cfs.

   If SJR flow @ Vernalis > 1000 cfs then,
   Banks_max_allow = Banks_standard_allow + 1/3*(SJR @ Vernalis)
   But only upto physical capacity.
   Banks minimum
**************************************/

 
! Number of days in which the Banks limit *IS* increased by 1/3 of SJR flow
define BanksDaysIncr {
    select   DaysIncr
    from    BanksLimits
    where   month=month
}

! Number of days in which the Banks limit *IS NOT* increased by 1/3 of SJR flow
define BanksDaysNoIncr { value daysin - BanksDaysIncr}

! Finds the Max Pump Capacity with the allowable increase
!define BanksCapPermit2 {select PermitCap2 from BanksLimits where month=month}
if int(PBwheel_ON) == 1 {	!D1485
	define BanksMax1 {
		case Increase {
			 condition  AD_SJR_VAMP > 1000.0
			 value min(BanksCapPermit + AD_SJR_VAMP/3.0, BanksCapPhys) } !BanksCapPermit2) }
	!         value      max(BanksCapPermit,
	!                        min(BanksCapPermit1 + AD_SJR_VAMP/3.0, BanksCapPermit2) - EWAReservedCap) }
		case NoIncrease {
			 condition  always
			 value      BanksCapPermit }
	}
}
if int(PBwheel_ON) == 0 {	!D1641
	define BanksMax1 {
		case Increase {
			 condition  AD_SJR_VAMP > 1000.0
	!		 value min(BanksCapPermit + AD_SJR_VAMP/3.0, BanksCapPermit2) }
	         value      max(BanksCapPermit,
	                        min(BanksCapPermit1 + AD_SJR_VAMP/3.0, BanksCapPermit2) - EWAReservedCap) }
		case NoIncrease {
			 condition  always
			 value      BanksCapPermit }
	}
}
	
! Limit Pumping to allowable, weight for days in which increase is allowed
define BanksMaxAllow {value BanksMax1*BanksDaysIncr/daysin + BanksCapPermit*BanksDaysNoIncr/daysin }

! Limit Pumping to Allowable and Physical Capacity, whichever is less
goal banksLessThanAllow { D_Banks_swp_TD + D_Banks_cvp_TD + D_Banks_WTS_TD1 < BanksMaxAllow}
goal banksLessThanPhys  { D_Banks < BanksCapPhys}

! Output Allowable Pumping Cap for Checking Only
define BanksAllowOut {alias BanksMaxAllow kind 'EXPORT-CTRL-BANKS' UNITS 'CFS' }

! Banks PP Minimum Cap
define banksminpump {value 300.}

goal setbanksminpump {
    lhs D_Banks_SWP
    rhs banksminpump
    lhs>rhs penalty 0
    lhs<rhs penalty 2700
}

