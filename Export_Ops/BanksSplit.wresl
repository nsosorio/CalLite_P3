/*************************************
BanksSplit.wresl
 
Nazrul Islam DWR (04/27/2010)

Banks Split between SWP and CVP exports thru Bank PP. 
CVP exports include Cross valley canal (CVC) and CVP Wheelings
and CVP dedicated export.
**************************************/

define D_Banks_swp {std KIND 'FLOW-DELIVERY' UNITS 'CFS'} ! SWP Banks

define D_Banks_cvp {std kind 'FLOW-DELIVERY' UNITS 'CFS'} ! Total CVP wheeling at Banks
define D_Banks_EXP1 {std kind 'flow-delivery' units 'cfs'}
define D_Banks_EXP2 {std kind 'flow-delivery' units 'cfs'}
define D_Banks_cvc {std kind 'FLOW-DELIVERY' UNITS 'CFS'} ! CVC wheeling at Banks
define D_Banks_cvcrels {std kind 'FLOW-DELIVERY' UNITS 'CFS'} ! CVC - Released Water
define D_Banks_cvcsurp {std kind 'FLOW-DELIVERY' UNITS 'CFS'} ! CVC - Surplus Only
define D_Banks_cvcsurp1 {std kind 'FLOW-DELIVERY' UNITS 'CFS'}
define D_Banks_cvcsurp2 {std kind 'FLOW-DELIVERY' UNITS 'CFS'}

define D_Banks_cvpwhl {std kind 'FLOW-DELIVERY' UNITS 'CFS'} ! CVP Wheeling to CVP San Luis
define D_Banks_whlrels {std kind 'FLOW-DELIVERY' UNITS 'CFS'} ! JPOD - Released Water
define D_Banks_whlsurp {std kind 'FLOW-DELIVERY' UNITS 'CFS'} ! JPOD - Surplus Only
define D_Banks_whlsurp1 {std kind 'FLOW-DELIVERY' UNITS 'CFS'}
define D_Banks_whlsurp2 {std kind 'FLOW-DELIVERY' UNITS 'CFS'}

/**
define D_Banks_cvpded {std kind 'FLOW-DELIVERY' UNITS 'CFS'} !CVP dedicated exports
define D_Banks_cvpded_Exp1 { std kind 'FLOW-DELIVERY' UNITS 'CFS'}
define D_Banks_cvpded_Exp2 { std kind 'FLOW-DELIVERY' UNITS 'CFS'}
**/

define Surp1Terms {std kind 'FLOW-CHANNEL' units 'CFS'} ! Used in COA calculations
define Surp2Terms {std kind 'FLOW-CHANNEL' units 'CFS'}
define SurpTerms {std kind 'FLOW-CHANNEL' units 'CFS'}

goal totalBanks {D_Banks = D_Banks_swp + D_Banks_cvp} 
goal SWPArcSplit {D_Banks_SWP = D_Banks_EXP1 + D_Banks_EXP2 }

if int(PBwheel_ON)==1{
	define D_Banks_whlpb {std kind 'FLOW-DELIVERY' UNITS 'CFS'} ! CVP payback wheeling at Banks (D-1485)
	goal totalcvpWheel {D_Banks_cvp = D_Banks_whlpb + D_Banks_cvc + D_Banks_cvpwhl}	 ! Total Wheeling at banks (payback + CVC + CVP wheeling to CVP San Luis).
	objective ObjWhlpb {[D_Banks_whlpb,100]}
}

if int(PBwheel_ON)==0{
	goal totalcvpWheel {D_Banks_cvp = D_Banks_cvc + D_Banks_cvpwhl}	 ! Total Wheeling at banks (CVC + CVP wheeling to CVP San Luis).
}

goal totaljpodwhl {D_Banks_cvpwhl = D_Banks_whlrels + D_Banks_whlsurp}
goal totalcvpwhlsurp {D_Banks_whlsurp = D_Banks_whlsurp1 + D_Banks_whlsurp2}
goal SetSurp1Terms {Surp1Terms = D_Banks_cvcsurp1 + D_Banks_whlsurp1}
goal SetSurp2Terms {Surp2Terms = D_Banks_cvcsurp2 + D_Banks_whlsurp2}

goal totalcvcwhl {D_Banks_cvc = D_Banks_cvcrels + D_Banks_cvcsurp}
goal totalcvcsurp {D_Banks_cvcsurp = D_Banks_cvcsurp1 + D_Banks_cvcsurp2}

goal SetSurpTerms {SurpTerms = Surp1Terms + Surp2Terms}