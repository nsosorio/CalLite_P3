/*************************************
exportratio.wresl
 
Nazrul Islam DWR (04/27/2010)

Export-Inflow Ratio restriction on exports

Z.Q. Richard Chen, DWR 02/07/2011 
Set EI_DLTSW switch to control the ExpRatio which changes with month.
Hao Xie, DWR 09/11/2011
Modify Inflow by considering Hood dicu
**************************************/

! Delta Export defined as in DWRSIM Algorithm Description for Export Ratio
!define ExportActual {alias  D_Jones+D_Banks KIND 'EXPORT-PRJ' units 'CFS'  }
define ExportActualTD {alias D_Jones_TD+D_Banks_TD KIND 'EXPORT-PRJ' units 'CFS'  }
define ExportActualIF {alias D_Jones_IF+D_Banks_IF KIND 'EXPORT-PRJ' units 'CFS'  }

! Calculate 3 day running average
define ExportActualTD_2daysum {sum(i=-2,-1,1) ExportActualTD(i)}
define ExportActualTD_3dayavg {std kind 'EXPORT-PRJ' units 'CFS'}
goal set_ExportActualTD_rollavg { ExportActualTD_3dayavg = (ExportActualTD_2daysum +  ExportActualTD)/3 }

! Delta Inflow defined as in DWRSIM Algorithm Description for Export Ratio
define Inflow {alias C_Hood_ANN
					+C_YoloBP
					+AD_Mokelumne+AD_SJRCalALL
					+I_MarshCr
					+C_SJRVER
					+D_Hood_NP-I_HOOD kind 'INFLOW-DELTA' UNITS 'CFS'
}

! Calculate 14 day running average
define Inflow_13daysum {sum(i=-13,-1,1) Inflow(i)}
define Inflow_14dayavg {std kind 'EXPORT-PRJ' units 'CFS'}
goal set_Inflow_rollavg { Inflow_14dayavg = (Inflow_13daysum +  Inflow)/14 }

define Inflow_2daysum {sum(i=-2,-1,1) Inflow(i)}
define Inflow_3dayavg {std kind 'EXPORT-PRJ' units 'CFS'}
goal set_Inflow_3dayrollavg { Inflow_3dayavg = (Inflow_2daysum +  Inflow)/3 }

! EI allowable export variable - MAXIMUM ALLOWABLE EXPORT due to EXPORT RATIO
define EiExpCtrl {std kind 'EXPORT-CTRL-EI' units 'CFS'}

define January_EightRiverIndex {
		select  pmi_0
		from    eightriver
		where   wateryear=wateryear}
!define JanEightRiverIndex {value January_EightRiverIndex/1000} ! UPDATE ONCE CALCULATED JAN 8RI HISTORICAL DATA, NSO 01/20/2022

define febeiratio {
	case LTEQ1MAF {
		condition January_EightRiverIndex <= 1000.
		value 0.45
	}
	case GT {
		condition January_EightRiverIndex > 1500.
		value 0.35
	}
	case otherwise {
		condition always
		value 0.40
	}
}

! EI Ratio dependent on month. Only February can vary depending on 8RI
define ExpRatio {     !EI_DLTSW switch added 10/04/2010 by Z.Q. Richard Chen, DWR
    case NOREGULATION {
      condition EI_DLTSW == 0
      value 1}
    case USERDEF {  
      condition EI_DLTSW == 2
        select ratio
        from gui_EiRatio 
        where month=month }
    case feb    {       !.and. EI_DLTSW== 1
        condition month == FEB ! UPDATE ONCE CALCULATED JAN 8RI HISTORICAL DATA, NSO 01/20/2022
        	value febeiratio}
    case rest   {       !.and. EI_DLTSW== 1
        condition always
          select    ratio
          from      eiratio
          where     month=month}
}

! Write out EI ratio
define ExpRatio_ {alias ExpRatio KIND 'EI-RATIO-STD' UNITS 'NONE'}

! Compute exports allowable by the EI ratio
! Delta inflow is a 14-day running average, except when the CVP or the SWP is making storage withdrawals for export
! in which case both the export rate and the Delta inflow are 3-day running averages.
goal find_max_export { lhs EiExpCtrl
		case first{
            condition wateryear == BgnWY .and. month == Oct
            rhs ExpRatio*Inflow_14dayavg}
		case CVP_SWP_SWD {
			condition SHADS(-1) > 0.1 .or. FOLDS(-1) > 0.1 .or. SWPDS(-1) > 0.1
			rhs ExpRatio*Inflow_3dayavg}
		case otherwise {
			condition always
			rhs ExpRatio*Inflow_14dayavg}		
}

! Restrict exports to be less than that allowable by EI ratio
! For the calculation of maximum percent Delta inflow diverted, the export rate is a 3-day running average
goal export_comply { ExportActualTD_3dayavg < EiExpCtrl }

!Export-San Joaquin River Inflow Ratio    added by Richard Chen 4/11/2011
define esjrir_o    { 
    case EISJR_UDEF 
      {condition EISJR_DLTSW == 2 
       select offset from gui_eisjr where wyT_Sac=wyT_Sac,month=month }
    case NO_ESJRIR
      {condition always
       value 999999 }
    }
    
define esjrir_m    { 
    case EISJR_UDEF 
      {condition EISJR_DLTSW == 2
       select multiplier from gui_eisjr where wyT_Sac=wyT_Sac,month=month }
    case NO_ESJRIR
      {condition always
       value 0.0 }
    }
    

    
!Max South Delta Exports <= "offset" + "multiplier" * SJR Inflow at Vernalis
!goal export_SJRIR_comply { ExportActual   <  esjrir_o + esjrir_m*AD_SJR }  

goal export_SJRIR_comply { lhs ExportActualTD 
        case EISJR_UDEF 
             {condition EISJR_DLTSW == 2
             rhs esjrir_o + esjrir_m*VERNQ
             lhs<rhs penalty 0
             lhs>rhs penalty 999999 
             }
             }



                            
                           

