/*************************************
NMFS_SALMON_BO_SJR.wresl
 
Hao Xie   DWR (09/03/2010)

NMFS BO Action 4.2.1
**************************************/

!whether implement Action 4.2.1: no action: value=0; action: value=1
!define Action_4_2_1 {select Option from options where Index=3}

!*********************************************************************************************
! Phase I: Interim export restriction under Action 4.2.1
/*
define SJRatio_MaxExp{
        case Flood_flow{
                condition Vernalis > 21750.    !daily flow condition - should use an equivalent monthly for this level of peak flow
                value 999999.
                }
        case High_flow{
                condition Vernalis >= 6000.
                value Vernalis / 4.
                }
        case Interim{
                condition always
                value 1500.
                }
       }
*/
!*********************************************************************************************
! Phase II: Export restriction under Action 4.2.1
!
! Determine year typing as defined in the NMFS BO
/* Exception procedure for multiple dry years:
 * If the previous 2 years plus current year of San Joaquin Valley “60-20-20” Water Year Hydrologic Classification and Indicator
 * as defined in D-1641 and provided in following table, is 6 or less,
 * AND the New Melones Index is less than 1 MAF,
 * exports shall be limited to a 1:1 ratio with San Joaquin River inflow, as measured at Vernalis.
 * Reclamation and DWR shall implement the Vernalis flow-to-combined export ratios based on a 14-day running average.
 */
define SJR_indicator  {value 6. - wyt_SJR}
define SJR_indicator1 {value 6. - wyt_SJR_prv1}
define SJR_indicator2 {value 6. - wyt_SJR_prv2}

define NMforecast1SV {timeseries kind 'forecast' units 'taf'}

define Vernalis_14daysum {sum(i=-13,0,1) Vernalis(i)} ! Vernalis is currently state variable so this is possible.
! If SJR becomes dynamic, will have to update this equation because current Vernalis flow is a DV, not SV.
define Vernalis_14dayavg {value Vernalis_14daysum/14}

define SJRatio_MaxExp{
    case EISJR_UDEF {                             
         condition EISJR_DLTSW == 2 .or. int(IF_SWPorCVP_OnOff) == 1
         value 999999. 
         }
    case Flood_flow{
        condition Vernalis >= 21750.    !daily flow condition - should use an equivalent monthly for this level of peak flow
        value 999999.
        }
    case mult_drought{
        condition (SJR_indicator + SJR_indicator1 + SJR_indicator2) <= 6.  .AND. NMforecast1SV <= 1000
        value Vernalis_14dayavg
        }
    case critical{
        condition wyt_SJR == 5
        value Vernalis_14dayavg
        }
        case dry{
        condition wyt_SJR == 4
        value Vernalis_14dayavg / 2.
        }
    case BelowN{
        condition wyt_SJR == 3
        value Vernalis_14dayavg / 3.
            }
    case AN_Wet{
        condition always !wyt_SJR 2 and 1
        value Vernalis_14dayavg / 4.
        }
}

!*********************************************************************************************
define SJRatio_MaxExpdv {alias SJRatio_MaxExp kind 'NMFS' units 'NONE'}

define NMFS_MaxExp {
    case AprMay {
        condition month>=APR .AND. month<=MAY .and. Action_4_2_1==1
        value SJRatio_MaxExp}
    case otherwise{
        condition always
        value 999999.}
}
define NMFS_MaxExpdv {alias NMFS_MaxExp kind 'NMFS' units 'cfs'}

