/*************************************
Banks_Limits.wresl
 
Nazrul Islam DWR (04/27/2010)


This is the permit export (standard allowable) and physical capacity for Banks.
Banks Permitted Pumping Amount with dynamic EWA capacity dedication
Even though EWA regulation is not modeled, but the EWA capacity is modeled in CalSim
**************************************/

 
define BanksCapPermit1 {
	case IFon {
		condition banksifswitch > 0.5 .or. jonesifswitch > 0.5
		select PermitCap1_BDCP from BanksLimits where month=month}
	case D1485 {
		condition PUMP_DLTSW == 3
		select PermitCap1_D1485 from BanksLimits where month=month}
	case D1641 {
		condition always
		select PermitCap1_D1641 from BanksLimits where month=month}
}
		
define BanksCapPermit2 {
	case IFon {
		condition banksifswitch > 0.5 .or. jonesifswitch > 0.5
		select PermitCap2_BDCP from BanksLimits where month=month}
	case IFoff {
		condition always
		select PermitCap2 from BanksLimits where month=month}
}

define EWAReservedCap {select EWACap from BanksLimits where month=month}

! Permitted capacity (Standard Allowable)
define BanksCapPermit {value min(BanksCapPermit1, BanksCapPermit2 - EWAReservedCap) }

! Banks Physical Capactiy
define BanksCapPhys {select PhysCap from BanksLimits where month=month }

! CVP dedicated capacity
define BanksMaxAllowCVP {select CVPDedCap from BanksLimits where month=month}
define BanksMaxCVPdv {alias BanksMaxAllowCVP kind 'flow' units 'CFS'}
