!WRESL code for Vernalis Water Quality
!VERNALIS_WQMIN
! Edward Chang
! 5/01/2000
! Modified by Ken Yokoyama,USBR, April 15,2003 for Link-Node method
!
!The following code calculates the required flows below Goodwin Dam to meet the Vernalis
!water quality requirement
!*****************************************************************************************
!The Vernalis water quality requirement is an Electrical Conductivity (EC) requirement of
!700 and 1000 micromhos/cm for the irrigation (Apr-Aug) and non-irragation (Sep-Mar) seasons,
!respectively.  This is different from the irrigation and non-irrigation season used to label
!the salinities of Goodwin, Accretions, main-stem, and west-side return flows (which use an
!irrigation season of March thru September.  The EC of the flows without Goodwin Dam flows
!are calculated by combining the flows and salinity from the Stanislaus river accretions and
!returns, main-stem flows of the San Joaquin river before confluence with the Stanislaus river,
!and the west-side return flows.  Based on these flows and salinities, and a known salinity for
!Goodwin flows, a flow requirement is calculated at Goodwin to ensure that the water quality
!requirement at Vernalis is met.
!
!******************************************************************************************
!Cycle 2 (Water Quality for non-pulse period)
! *********************************************************************************************************************************
!
! This is the link-node salinity algorithm covering the San Joaquin River from Lander Ave. to Vernalis.  The link-node algorithm
! consists of a series of EC mass balance equations from node 614 to node 639.  The computed EC from an upstream node is used as
! the input EC of a downstream node.  Flow-EC regressions are used for the San Joaquin River at Lander Ave., Merced River near
! Stevinson and the Tuolumne River near Modesto. Mud and Salt Slough, return Flow and accretion EC use monthly average values.
! Written by Ken Yokoyama and Nancy Parker, April 2003

/*Tributary Boundary Conditions to the link-node domain
EC[microS/cm] = f(flow[cfs]), subject to max/min constraints
(i.e. Regression Equations defining EC as a function of flow for the following tribs:
-- San Joaquin River at Lander Ave.
-- Merced River near Stevinson
-- Tuolumne River near Modesto)
Equation parameters developed by D. Steiner - 7/29/04, reflecting ~WY2000-2004 observations
Max/min constraints on EC at each boundary were proposed by D. Steiner based on the same
recent observations.
*/
define Lan_WQparam_1 {value -239.45}
define Lan_WQparam_2 {value 2101.5}
define Mer_WQparam_1 {value -100.52}
define Mer_WQparam_2 {value 729.58}
define Tuo_WQparam_1 {value -57.018}
define Tuo_WQparam_2 {value 500.75}

!A. Fock 2005/09/19 define EC_611 {value min(2000., max(500., Lan_WQparam_1*log(max(1., C_SJRLNDR[SJRBASE])) + Lan_WQparam_2))} ! SJR at Lander Ave:

define EC_MERCED2 {value min(500., max(85., Mer_WQparam_1*log(max(1.,C_MERCED2[SJRBASE])) + Mer_WQparam_2))}! Merced at Stevinson
define EC_TUOL {value min(275., max(85., Tuo_WQparam_1*log(max(1.,C_TUOL[SJRBASE])) + Tuo_WQparam_2))}! Tuolumne at Modesto

!Start of the link-node method.
define EC_605 { value EC_C_GRAVF }

define EC_595 {
    case NearZero {
        condition C_Chowbyp2[SJRBASE] + D_Chowbyp[SJRBASE] + L_Chowbyp[SJRBASE] <0.0001
        value 0.0 }
    case otherwise {
        condition always
        value (C_Chowbyp1[SJRBASE] * EC_605
            + C_FRSNO[SJRBASE] * EC_C_FRSNO
            + R_Chowbyp[SJRBASE] * EC_R_Chowbyp)
            / (C_Chowbyp2[SJRBASE] + D_Chowbyp[SJRBASE] + L_Chowbyp[SJRBASE] )
            }}

define EC_607 {
    case NearZero {
        condition C_MDOTA[SJRBASE] + C_MDOTABC[SJRBASE] + D_MDOTAA[SJRBASE] + D_MDOTAD[SJRBASE] + L_MDOTA[SJRBASE] < 0.0001
        value 0.0 }
    case otherwise {
        condition always
        value (C_SJRCB[SJRBASE] * EC_605
            + I_MDOTA * EC_I_MDOTA
            + C_CVPDMC[SJRBASE] * EC_Mendota_Pool
            + R_MDOTA[SJRBASE] * EC1_R_MDOTAWest )
            / (C_MDOTA[SJRBASE] + C_MDOTABC[SJRBASE] + D_MDOTAA[SJRBASE] + D_MDOTAD[SJRBASE] + L_MDOTA[SJRBASE])}
            }

define EC_608 {
    case NearZero {
        condition C_SJRSACK[SJRBASE] + L_SJRREST[SJRBASE] + C_SJRSACKBC[SJRBASE] < 0.0001
        value 0.0 }
    case otherwise {
        condition always
        value (C_MDOTA[SJRBASE] * EC_607
            + C_SJRREST[SJRBASE] * EC_605 )
            / (C_SJRSACK[SJRBASE] + L_SJRREST[SJRBASE] + C_SJRSACKBC[SJRBASE])}
            }

define EC_SJRSAND {
    case NearZero {
        condition C_SANDSL[SJRBASE] + C_SJRSAND[SJRBASE] + L_SJRSAND[SJRBASE] < 0.0001
        value 0.0 }
    case otherwise {
        condition always
        value (C_SJRSACK[SJRBASE] * EC_608)
            / (C_SANDSL[SJRBASE] + C_SJRSAND[SJRBASE] + L_SJRSAND[SJRBASE])}
            }

define EC_587 {
    case NearZero {

        condition C587[SJRBASE] + C_MPBYP[SJRBASE] + L_CHOWR2[SJRBASE] < 0.0001
        value 0.0 }
    case otherwise {

        condition always
        value (C_Chowbyp2[SJRBASE] * EC_595
            + C_CHOWR[SJRBASE] * EC_C_CHOWR
            + R_CHOWR2A[SJRBASE] * EC_R_CHOWR2A
            + R_CHOWR2B[SJRBASE] * EC_R_CHOWR2B
            + C_SANDSL[SJRBASE] * EC_SJRSAND)
            / (C587[SJRBASE] + C_MPBYP[SJRBASE] + L_CHOWR2[SJRBASE]) }
            }

define EC_589 {
    case NearZero {

        condition C_EASTBYP[SJRBASE] < 0.0001
        value 0.0 }
    case otherwise {

        condition always
        value (C587[SJRBASE] * EC_587
            + I_EASTBYP * EC_I_EASTBYP )
            / C_EASTBYP[SJRBASE] }
            }

define EC_SJRMPBYP {
    case NearZero {
        condition C_SJRMPBYP[SJRBASE] + L_SJRMPBYP[SJRBASE] < 0.0001
        value 0.0 }
    case otherwise {
        condition always
        value (C_SJRSAND[SJRBASE] * EC_SJRSAND
            + C_MPBYP[SJRBASE] * EC_587)
            / (C_SJRMPBYP[SJRBASE] + L_SJRMPBYP[SJRBASE])}
            }

define EC_611 {
    case NearZero {
        condition C_SJRLNDR[SJRBASE] + L_SJRLNDR[SJRBASE] < 0.0001
        value 0.0 }
    case otherwise {
        condition always
        value (C_EASTBYP[SJRBASE] * EC_589
            + C_SJRMPBYP[SJRBASE] * EC_SJRMPBYP)
            / (C_SJRLNDR[SJRBASE] + L_SJRLNDR[SJRBASE])}
            }

/*This latter term represents load closure at the first of two "control" points used to calibrated
the WQ Module:  "Above Newman" and "Maze".  At either control point, the simulated difference
is computed between the upstream explicits and the control point's flow-EC relationship as
observed during recent years.  This difference is accepted as a residual load (mostly related to
precipitation-runoff, but not completely), and gets injected into the module domain as a distributed
residual at nodes 614 and 620.  The distribution coefficients at nodes 614 and 620 were chosen
arbitrarily, defined in wq_defs_Disag.*/
!All loads entering into this mass balance are "explicit" except for "0.5*AbvNwmnResLoad"
define EC_614 {value (C_SJRLNDR[SJRBASE] * EC_611
               + I_SJRMSA * EC_I_SJRMSA
               + (I_SJRMS[SJRBASE]-I_SJRMSA) * EC_Grassland_B  ! I_SJRMS (Grasslands Bypass is currently lumped with Mud and Salt Slough)
               + R_SJRMS[SJRBASE] * EC1_R_SJRMs 
               + C_MDOTABVAMP[SJRBASE] * EC_Mendota_Pool
               + PctAt614*AbvNwmnResLoad)   !LDB, 8/30/04
               /C_SJRMS[SJRBASE]
               }

/* This mass balance represents Newman or the Merced/San Joaquin River confluence */
define EC_620 {value (C_SJRMS[SJRBASE] * EC_614
               + C_MERCED2[SJRBASE] * EC_MERCED2
               + C619[SJRBASE] * EC1_R_SJRMer
               + R_SJRMER[SJRBASE] * EC_E_Return620
               + PctAt620*AbvNwmnResLoad)          !LDB, 8/30/04
               /(C_SJRMER[SJRBASE]+D_SJRMERA[SJRBASE]+D_SJRMERB[SJRBASE]+D_SJRMERC[SJRBASE])    !LDB, 8/30/04
               }

/* This mass balance represents near Modesto or the Tuolumne/San Joaquin River confluence.
As with the "Above Newman" control, a load residual was computed at the "Maze" control and
reflects load residual between Newman and Maze.  This residual has been distributed
at nodes 630 and 636.*/
define EC_SJRTUOL {value (C_SJRMER[SJRBASE] * EC_620
               + C_TUOL[SJRBASE] * EC_TUOL
               + R_SJRTUOLWest[SJRBASE] * EC1_R_SJRTUOL
               + R_SJRTUOLJ[SJRBASE] * EC_TID_Return
               + R_SJRTUOLK[SJRBASE] * EC_TID_Return
               + R_SJRTUOLL[SJRBASE] * EC_TID_Return
               + R_SJRTUOLM[SJRBASE] * EC_R_SJRTUOLM
               + saltSJRTUOL  !A. Fock 2005/09/23 Salt from tile drains and GW base flow
               + PctAt630*BtwMazeNwmnResLoad) !LDB, 9/13/04
               /(C_SJRTUOL[SJRBASE]+D_SJRTUOLA[SJRBASE]+D_SJRTUOLB[SJRBASE])
               }

/* This mass balance represents Maze upstream of the Stanislaus/San Joaquin River confluence.
This EC now replaces the EC formerly computed by the Kratzer Equation */
define EC_SJRMAZE {value (C_SJRTUOL[SJRBASE] * EC_SJRTUOL
               + R_SJRMAZEA[SJRBASE] * EC_MID_Return
               + R_SJRMAZEB[SJRBASE] * EC_MID_Return
               + R_SJRMAZEC[SJRBASE] * EC_MID_Return
               + SaltSJRMaze !A. Fock 2005/09/23 Salt from tile drains and GW base flow
               + PctAt636*BtwMazeNwmnResLoad) !LDB, 9/13/04
               /(C_SJRMAZE[SJRBASE])
               }

/*This mass balance represents the Stanislaus River at Ripon */
define I_STANRIPNpos {value max(0.,I_STANRIPN)}
define I_STANRIPNneg {value min(0.,I_STANRIPN)}

define EC_STANRIPN {value (C_STANGDWN[SJRBASE] * EC_Goodwin
               + R_STANRIPNA[SJRBASE] * EC_Stan_Return !OID/SSJID
               + R_STANRIPNB[SJRBASE] * EC_Stan_Return !SSJID
               + R_STANRIPNC[SJRBASE] * EC_MID_Return
               + I_STANRIPNpos * EC_Stan_Accr)
               /(C_STANRIPN[SJRBASE]+D_STANRIPN[SJRBASE]-I_STANRIPNneg)
               }

/*This mass balance represents the Stanislaus/San Joaquin River confluence.
Note that potential creek loads between Maze-Ripon-Vernalis are not represented.*/
define EC_637 {value (C_SJRMAZE[SJRBASE] * EC_SJRMAZE
               + C_STANRIPN[SJRBASE] * EC_STANRIPN
               + I_SJRSTAN * EC_Stan_Rip_Rtn !LDB 10/22/04; Subjective; Creek Inflow EC = quality of Stanislaus Basin Return Flow
               + R_SJRSTANA[SJRBASE] * EC_MID_Return
               + R_SJRSTANB[SJRBASE] * EC_MID_Return
               + R_SJRSTANC[SJRBASE] * EC_Stan_Rip_Rtn
               + R_SJRSTAND[SJRBASE] * EC_R_SJRSTAND)
               /(C_SJRSTAN[SJRBASE]+D_SJRSTAN[SJRBASE])
               }

/* This mass balance represents Vernalis */
define EC_639 {value (C_SJRSTAN[SJRBASE] * EC_637
           + R_SJRVERA[SJRBASE] * EC_R_SJRVERA
           + R_SJRVERWest[SJRBASE] * EC1_R_SJRVER)
           /(C_SJRVER[SJRBASE]+D_SJRVER[SJRBASE])
               }

define Goodwin_Req_min1 { value  R_STANRIPNA[SJRBASE]*(VernWQstd - EC_Stan_Return)/(EC_Goodwin - VernWQstd)
                              + R_STANRIPNB[SJRBASE]*(VernWQstd - EC_Stan_Return)/(EC_Goodwin - VernWQstd)
                              + R_STANRIPNC[SJRBASE]*(VernWQstd - EC_MID_Return)/(EC_Goodwin - VernWQstd)
                              + I_STANRIPN*(VernWQstd - EC_Stan_Accr)/(EC_Goodwin - VernWQstd)
                              + C_SJRMAZE[SJRBASE]*(VernWQstd - EC_SJRMAZE)/(EC_Goodwin - VernWQstd)
                              + R_SJRSTANA[SJRBASE]*(VernWQstd - EC_MID_Return)/(EC_Goodwin - VernWQstd)
                              + R_SJRSTANB[SJRBASE]*(VernWQstd - EC_MID_Return)/(EC_Goodwin - VernWQstd) }

define Goodwin_Req_min2 { value R_SJRSTANC[SJRBASE]*(VernWQstd - EC_Stan_Rip_Rtn)/(EC_Goodwin - VernWQstd)
                              + R_SJRSTAND[SJRBASE]*(VernWQstd - EC_R_SJRSTAND)/(EC_Goodwin - VernWQstd)
                              + I_SJRSTAN*(VernWQstd - EC_Stan_Rip_Rtn)/(EC_Goodwin - VernWQstd)
                              + R_SJRVERA[SJRBASE]*(VernWQstd - EC_R_SJRVERA)/(EC_Goodwin - VernWQstd)
                              + R_SJRVERWest[SJRBASE]*(VernWQstd - EC1_R_SJRVER)/(EC_Goodwin - VernWQstd)
                              - D_SJRSTAN[SJRBASE]*(VernWQstd - EC_637)/(EC_Goodwin - VernWQstd)
                              - D_STANRIPN[SJRBASE]*(VernWQstd - EC_Goodwin)/(EC_Goodwin - VernWQstd)
                              }

define Goodwin_Req_min {value Goodwin_Req_min1 + Goodwin_Req_min2}

!define WQ_min_def { value   max(0.,Goodwin_Req_min-C_STANGDWN[SJRBASE]) }

define WQ_min_def { 
    case  D1641EC  {
    	condition   D1641_SALINITY == 1
	value   max(0.,Goodwin_Req_min-C_STANGDWN[SJRBASE]) }
    case otherwise {
              condition always
              value     0. }
          }

define WQ_reduced_min_def {
    case March {
        condition   month == mar
        value       max(0.,min ( WQRelCap * TAF_cfs, WQ_min_def )) }
    case april {
        condition   month == apr
        value       max(0.,min ( remWQrelCap(-1)*30./14. * TAF_cfs, WQ_min_def )) }
    case may {
        condition   month == may
        value       max(0.,min ( remWQrelCap(-1)*31./15. * TAF_cfs, WQ_min_def )) }
    case otherwise {
        condition   always
        value       max(0.,min ( remWQrelCap(-1) * TAF_cfs, WQ_min_def )) }
        }

goal capC_MELONwqcycle2 { C_MELONWQ < WQ_reduced_min_def }
!goal maintainC_MELONm1cycle2 { C_MELONm > max(0.,C_MELONm[SJRBASE]) }
goal maintainC_MELONm2cycle2 { C_MELONm = C_MELONm[SJRBASE]  }

define WQ_min_defdv { std kind 'flow-deficiency' units 'cfs' }
goal setWQ_min_defdv { WQ_min_defdv = WQ_min_def }
define WQ_REDmin_defdv { std kind 'flow-deficiency' units 'cfs' }
goal setWQ_REDmin_defdv { WQ_REDmin_defdv = WQ_REDUCED_min_def }
