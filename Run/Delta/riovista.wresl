/*************************************
riovista.wresl
 
Tom FitzHugh BOR 4/23/2010

Sets minimum flow for Rio Vista.  Minimum flow required in riovista.table is from WQCP 5/95 

Z.Q. Richard Chen, DWR 02/07/2010 
Set RV_DLTSW switch to control the Rio_Vista_req which changes with month and wyt_SAC.

*************************************/

define Rio_Vista_req {
    case USERDEF {  
      condition RV_DLTSW == 2
        select river_q  
        from  gui_riovista 
        where month=month, wyt_SAC=wyt_SAC }
	case IFon {
		condition banksifswitch > 0.5 .or. jonesifswitch > 0.5
			select river_q_BDCP
			from riovista
			where       month=month, wyt_SAC=wyt_SAC}
    case D1641 {  
      condition RV_DLTSW == 1
        select      river_q_D1641
        from        riovista
        where       month=month, wyt_SAC=wyt_SAC}
    case D1485 {  
      condition RV_DLTSW == 3
        select      river_q_D1485
        from        riovista
        where       month=month, wyt_SAC=wyt_SAC}
    case NOREGULATION {  
    condition always
    value 0}
}

        
define mif_RioVista {alias Rio_Vista_req KIND 'FLOW-REQ-RVISTA' UNITS 'CFS'}

goal minflow_RioVista {C_SacRV_MIF < Rio_Vista_req }

! Calculate 7 day running average
define C_SacRV_MIF_7daysum {sum(i=-6,-1,1) C_SacRV_MIF(i)}
define C_SacRV_MIF_7dayavg {std kind 'FLOW-MIN-INSTREAM' units 'CFS'}
goal set_RV_MIF_rollavg { C_SacRV_MIF_7dayavg = (C_SacRV_MIF_7daysum +  C_SacRV_MIF)/7 }

define C_SacRV_7daysum {sum(i=-6,-1,1) C_SacRV(i)}
define C_SacRV_7dayavg {std kind 'FLOW-MIN-INSTREAM' units 'CFS'}
goal set_RV_rollavg { C_SacRV_7dayavg = (C_SacRV_7daysum +  C_SacRV)/7 }

!"The 7-day running average shall not be less than 1,000 cfs below the monthly objective." NSO 01/21/2022
goal setbuffer {
	lhs C_SacRV_7dayavg	
	case D1641_SepOnRamp_OctDec {
		condition (month == Sep .and. day > 7) .or. range(month,oct,dec) ! Starting after 7 days because RV MIF was zero in August.
		rhs Rio_Vista_req - 1000.
		lhs>rhs penalty 0.		
	}	 
} 